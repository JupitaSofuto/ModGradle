plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
    maven { url = 'https://maven.moddingx.org' }
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://maven.parchmentmc.org' }
}

dependencies {
    implementation('com.google.code.findbugs:jsr305') { version { strictly '[3.0.2,)'; prefer '3.0.2' } }
    implementation('net.minecraftforge:srgutils') { version { strictly '[0.4.11,)'; prefer '0.4.11' } }
    implementation('net.minecraftforge.gradle:ForgeGradle') { version { strictly "[5.1.41,5.2)"; prefer '5.1.41' } }
    implementation('net.minecraftforge:artifactural') { version { strictly '[3.0.8,)'; prefer '3.0.8' } }
    compileOnly('org.parchmentmc:librarian') { version { strictly '[1.2.0,)'; prefer '1.2.0' } }
    implementation('de.siegmar:fastcsv') { version { strictly '[2.1.0,)'; prefer '2.1.0' } }
    implementation('com.google.code.gson:gson') { version { strictly '[2.9.0,)'; prefer '2.9.0' } }
    implementation('com.google.guava:guava') { version { strictly '[31.1-jre,)'; prefer '31.1-jre' } }
    implementation('org.apache.commons:commons-lang3') { version { strictly '[3.12.0,4.0.0)'; prefer '3.12.0' } }
    implementation('commons-io:commons-io') { version { strictly '[2.11.0,)'; prefer '2.11.0' } }
    implementation('org.apache.maven:maven-artifact') { version { strictly '[3.8.5,)'; prefer '3.8.5' } }
    implementation('org.moddingx:CurseWrapper') { version { strictly '[2.0,)'; prefer '2.0' } }

    // See https://cve.mitre.org/cgi-bin/cvename.cgi?name=CVE-2021-44228
    implementation('org.apache.logging.log4j:log4j-api') { version { strictly '[2.17.2,)' } }
    implementation('org.apache.logging.log4j:log4j-core') { version { strictly '[2.17.2,)' } }
}

gradlePlugin {
    plugins {
        mapping {
            id = 'org.moddingx.modgradle.mapping'
            implementationClass = 'org.moddingx.modgradle.plugins.mapping.MappingPlugin'
        }
        javadoc {
            id = 'org.moddingx.modgradle.javadoc'
            implementationClass = 'org.moddingx.modgradle.plugins.javadoc.JavadocPlugin'
        }
        meta {
            id = 'org.moddingx.modgradle.meta'
            implementationClass = 'org.moddingx.modgradle.plugins.meta.MetaPlugin'
        }
        sourcejar {
            id = 'org.moddingx.modgradle.sourcejar'
            implementationClass = 'org.moddingx.modgradle.plugins.sourcejar.SourceJarPlugin'
        }
        cursedep {
            id = 'org.moddingx.modgradle.cursedep'
            implementationClass = 'org.moddingx.modgradle.plugins.cursedep.CurseDepPlugin'
        }
        packdev {
            id = 'org.moddingx.modgradle.packdev'
            implementationClass = 'org.moddingx.modgradle.plugins.packdev.PackDevPlugin'
        }
        coremods {
            id = 'org.moddingx.modgradle.coremods'
            implementationClass = 'org.moddingx.modgradle.plugins.coremods.CoreModsPlugin'
        }
        mcupdate {
            id = 'org.moddingx.modgradle.mcupdate'
            implementationClass = 'org.moddingx.modgradle.plugins.mcupdate.McUpdatePlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url rootProject.file('../moddingx/maven')
        }
    }
}
